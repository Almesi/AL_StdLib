' Loops through a folder and Builds a single Module for each Folder
Sub AL_BuildApplication(ByVal FolderPath As String, ByVal OGFilePath As String, ByVal InstanceType As Integer)

    ' Create a FileSystemObject
    If AL_Check_String(FolderPath, 0) = False Or AL_Check_String(OGFilePath, 0) = False Then
        Exit Sub
    End If
    If AL_Check_Integer(InstanceType) = False Then
        Exit Sub
    End If

    Dim fso As Object
    Dim Folder As Object
    Dim SubFolder As Object
    Dim File As Object
    Dim InstanceName As String
    Dim VBProj As VBIDE.VBProject
    Dim VBComp As VBIDE.VBComponent
    Dim VBCodeModule As VBIDE.CodeModule

    Set fso = CreateObject("Scripting.FileSystemObject")

    ' Get the Folder object
    Set Folder = fso.GetFolder(FolderPath)

    ' Checks if Instance already exists
    InstanceName = fso.GetBaseName(FolderPath)
    Set VBProj = ThisWorkbook.VBProject
    For Each VBComp In VBProj.VBComponents
        If VBComp.Name = InstanceName Then
                AL_Error_Print 2, 3, InstanceName
                AL_Error_Show 2, 3, InstanceName
                Exit Sub
        End If
    Next VBComp

    ' Create an Instance (Module, Class, Userform)
    Select Case InstanceName
        Case "AL_Modules"
            InstanceType = 1
        Case "AL_Classes"
            InstanceType = 2
        Case "AL_Userforms"
            InstanceType = 3
    End Select
    AL_Include_CreateInstance InstanceType, InstanceName
    Set VBComp = VBProj.VBComponents(InstanceName)
    Set VBCodeModule = VBComp.CodeModule

    ' Loop through each File in the Folder
    For Each File In Folder.Files
        If File.Name Like "AL_*" Then
                AL_Include_AddCode VBCodeModule, File.Path
        End If
    Next File
    
    ' Recursively loop through each SubFolder
    For Each SubFolder In Folder.SubFolders
        If SubFolder.Name Like "AL_*" Then
            AL_BuildApplication SubFolder.Path, FolderPath, InstanceType
        End If
    Next SubFolder

End Sub