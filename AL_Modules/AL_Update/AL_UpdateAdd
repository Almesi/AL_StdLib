Sub AL_UpdateAdd(ByVal FilePath As String, ByVal InstanceType As Integer, ByVal InstanceName As String)
    Dim VBProj As VBIDE.VBProject
    Dim VBComp As VBIDE.VBComponent
    Set VBProj = ThisWorkbook.VBProject

    ' 0 means add code
    ' 1, 2, 3, 11, 100 means add Instance
    If AL_CheckString(InstanceName, 0) = True Then
        Select Case InstanceType
            Case 0
                Dim FileLine As String
                Dim Index As Long
                Dim FileNumber As Integer
                Dim VBCodeModule As VBIDE.CodeModule

                If AL_CheckInstance(InstanceName, False) = False Then
                    Exit Sub
                End If
                Set VBComp = VBProj.VBComponents(InstanceName)
                Set VBCodeModule = VBComp.CodeModule

                Index = 1
                FileNumber = FreeFile
                Open FilePath For Input As #FileNumber

                Do Until EOF(FileNumber)
                    Line Input #FileNumber, FileLine
                    VBCodeModule.InsertLines Index, FileLine
                    Index = Index + 1
                Loop
                Close #FileNumber
            Case 1, 2, 3, 11, 100
                If AL_CheckInstance(InstanceName, True) = False Then
                    Exit Sub
                End If
                Set VBComp = vbProj.VBComponents.Add(InstanceType)
                VBComp.Name = InstanceName 
            Case Else
                AL_ErrorShow 1, 2, InstanceType, InstanceName
                Exit Sub
        End Select
    End If
    
End Sub