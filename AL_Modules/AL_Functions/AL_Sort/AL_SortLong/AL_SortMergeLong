Sub AL_SortMergeLong(ByRef Array1() As Long, ByVal LowestIndex As Long, ByVal HighestIndex As Long)

    ' Check if there are at least two elements in the array to sort
    If LowestIndex < HighestIndex Then
        Dim ArrayMid As Long
        ArrayMid = Int((LowestIndex + HighestIndex) / 2)

        ' Recursively sort the left and right halves of the array
        AL_SortMergeInt Array1, LowestIndex, ArrayMid
        AL_SortMergeInt Array1, ArrayMid + 1, HighestIndex

        ' Merge the sorted halves of the array
        AL_MergeInt Array1, LowestIndex, ArrayMid, HighestIndex
    End If
End Sub

Sub AL_MergeLong(ByRef Array1() As Long, ByVal LowestIndex As Long, ByVal ArrayMid As Long, ByVal HighestIndex As Long)

    ' Calculate the sizes of the left and right subarrays
    Dim LeftSize As Long
    Dim RightSize As Long
    LeftSize = (ArrayMid - LowestIndex + 1)
    RightSize = (HighestIndex - ArrayMid)

    ' Declare arrays to hold the left and right subarrays
    Dim LeftArray() As Long
    Dim RightArray() As Long
    ReDim LeftArray(LeftSize - 1)
    ReDim RightArray(RightSize - 1)

    ' Copy data from the original array to the left and right subarrays
    Dim i As Long
    Dim j As Long
    Dim k As Long
    For i = 0 To LeftSize - 1
        LeftArray(i) = Array1(LowestIndex + i)
    Next i
    For j = 0 To RightSize - 1
        RightArray(j) = Array1(ArrayMid + j + 1)
    Next j

    ' Merge the left and right subarrays back into the original array
    i = 0
    j = 0
    k = LowestIndex
    Do While i < LeftSize And j < RightSize
        If LeftArray(i) <= RightArray(j) Then
            Array1(k) = LeftArray(i)
            i = i + 1
        Else
            Array1(k) = RightArray(j)
            j = j + 1
        End If
        k = k + 1
    Loop

    ' Copy any remaining elements from the left subarray
    Do While i < LeftSize
        Array1(k) = LeftArray(i)
        i = i + 1
        k = k + 1
    Loop

    ' Copy any remaining elements from the right subarray
    Do While j < RightSize
        Array1(k) = RightArray(j)
        j = j + 1
        k = k + 1
    Loop
End Sub